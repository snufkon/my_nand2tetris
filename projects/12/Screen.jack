// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * Graphic screen library.
 */
class Screen {
    static int baseAddress;
    static Array twoToThe;
    static boolean blackColor;

    /** Initializes the Screen. */
    function void init() {
      let baseAddress = 16384;
      let blackColor = true;

      let twoToThe = Array.new(16);
      let twoToThe[0]  =           1;
      let twoToThe[1]  =           2;
      let twoToThe[2]  =           4;
      let twoToThe[3]  =           8;
      let twoToThe[4]  =          16;
      let twoToThe[5]  =          32;
      let twoToThe[6]  =          64;
      let twoToThe[7]  =         128;
      let twoToThe[8]  =         256;
      let twoToThe[9]  =         512;
      let twoToThe[10] =        1024;
      let twoToThe[11] =        2048;
      let twoToThe[12] =        4096;
      let twoToThe[13] =        8192;
      let twoToThe[14] =       16384;
      let twoToThe[15] = 16384+16384; // to avoid 'Integer constant too big' error

      return;
    }

    /** Erases the whole screen. */
    function void clearScreen() {
      var Array screen;
      var int i;

      let i = 0;
      let screen = baseAddress;
      while (i < 8192) {
        let screen[i] = 0;
        let i = i + 1;        
      }
      
      return;
    }

    /** Sets the color to be used in further draw commands
     *  where white = false, black = true. */
    function void setColor(boolean b) {
      let blackColor = b;
      return;
    }

    function int getWordAddress(int x, int y) {
      return baseAddress + (y*32) + (x/16);
    }

    function int mod(int a, int b) {
      return a - ((a / b) * b);
    }

    function int getTargetBitLocation(int x) {
      return Screen.mod(x, 16);
    }

    function void updateTargetBit(int wordAddress, int targetBit) {
      var int value;
      
      let value = Memory.peek(wordAddress);
      if (blackColor) {
        let value = value | twoToThe[targetBit];
      } else {
        let value = value & ~twoToThe[targetBit];
      }
      do Memory.poke(wordAddress, value);

      return;
    }

    /** Draws the (x, y) pixel. */
    function void drawPixel(int x, int y) {
      var int wordAddress, targetBit, value;

      let wordAddress = Screen.getWordAddress(x, y);
      let targetBit = Screen.getTargetBitLocation(x);

      let value = Memory.peek(wordAddress);
      if (blackColor) {
        let value = value | twoToThe[targetBit];
      } else {
        let value = value & ~twoToThe[targetBit];
      }
      do Memory.poke(wordAddress, value);
      
      return;
    }

    function void drawHorizontalLine(int x1, int x2, int y) {
      var int startWordAddress, endWordAddress;
      var int startTargetBit, endTargetBit;
      var int i, max, temp;
      var Array screen;
      
      if (x1 > x2) {
        let temp = x1;
        let x1 = x2;
        let x2 = temp;
      }

      let startWordAddress = Screen.getWordAddress(x1, y);
      let startTargetBit = Screen.getTargetBitLocation(x1);
      let endWordAddress = Screen.getWordAddress(x2, y);
      let endTargetBit = Screen.getTargetBitLocation(x2);

      if (startWordAddress = endWordAddress) {
        let i = startTargetBit;
        let max = endTargetBit;
        while (~(i > max)) {
          do Screen.updateTargetBit(startWordAddress, i);
          let i = i + 1;          
        }
      } else {
        let i = startTargetBit;
        while (i < 16) {
          do Screen.updateTargetBit(startWordAddress, i);
          let i = i + 1;          
        }

        let i = 0;
        while (~(i > endTargetBit)) {
          do Screen.updateTargetBit(endWordAddress, i);
          let i = i + 1;
        }
        let i = startWordAddress + 1;
        let max = endWordAddress;

        let screen = 0;
        while (i < max) {
          if (blackColor) {
            let screen[i] = -1;
          } else {
            let screen[i] = 0;
          }
          let i = i + 1;
        }
      }
      
      return;
    }

    function void drawVerticalLine(int y1, int y2, int x) {
      var int i, max;

      if (y1 < y2) {
        let i = y1;
        let max = y2;
      } else {
        let i = y2;
        let max = y1;
      }

      while (~(i > max)) {
        do Screen.drawPixel(x, i);
        let i = i + 1;
      }

      return;
    }


    /** Draws a line from (x1, y1) to (x2, y2). */
    function void drawLine(int x1, int y1, int x2, int y2) {
      var int dx, dy, a, b, adyMinusbdx;

      let dx = x1 - x2;
      let dy = y1 - y2;

      if (dx = 0) {
        do Screen.drawVerticalLine(y1, y2, x1);
        return;
      }
      if (dy = 0) {
        do Screen.drawHorizontalLine(x1, x2, y1);

        return;
      }
      let adyMinusbdx = 0;
      if (dx < 0 & dy > 0) {
        let dx = -dx;
        while (~(a > dx) & ~(b > dy)) {
          do Screen.drawPixel(x1+a, y1-b);
          if (adyMinusbdx < 0) {
            let a = a + 1;
            let adyMinusbdx = adyMinusbdx + dy;
          } else {
            let b = b + 1;
            let adyMinusbdx = adyMinusbdx - dx;
          }
        }
        return;
      }
      if (dx < 0 & dy < 0) {
        let dx = -dx;
        let dy = -dy;
        
        while (~(a > dx) & ~(b > dy)) {
          do Screen.drawPixel(x1+a, y1+b);
          if (adyMinusbdx < 0) {
            let a = a + 1;
            let adyMinusbdx = adyMinusbdx + dy;
          } else {
            let b = b + 1;
            let adyMinusbdx = adyMinusbdx - dx;
          }
        }
        return;
      }
      if (dx > 0 & dy < 0) {
        let dy = -dy;
        
        while (~(a > dx) & ~(b > dy)) {
          do Screen.drawPixel(x1-a, y1+b);
          if (adyMinusbdx < 0) {
            let a = a + 1;
            let adyMinusbdx = adyMinusbdx + dy;
          } else {
            let b = b + 1;
            let adyMinusbdx = adyMinusbdx - dx;
          }
        }
        return;
      }
      while (~(a > dx) & ~(b > dy)) {
        do Screen.drawPixel(x1-a, y1-b);
        if (adyMinusbdx < 0) {
          let a = a + 1;
          let adyMinusbdx = adyMinusbdx + dy;
        } else {
          let b = b + 1;
          let adyMinusbdx = adyMinusbdx - dx;
        }
      }
    
      return;
    }

    /** Draws a filled rectangle where the top left corner
     *  is (x1, y1) and the bottom right corner is (x2, y2). */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
      var int j;
      let j = y1;

      while (~(j > y2)) {
        do Screen.drawHorizontalLine(x1, x2, j);
        let j = j + 1;
      }      
      
      return;
    }

    /** Draws a filled circle of radius r around (cx, cy). */
    function void drawCircle(int cx, int cy, int r) {
      var int dy, temp;
      let dy = -r;

      while (~(dy > r)) {
        let temp = Math.sqrt((r*r) - (dy*dy));
        do Screen.drawHorizontalLine(cx-temp, cx+temp, cy+dy);
        let dy = dy + 1;
      }
    
      return;
    }
}
